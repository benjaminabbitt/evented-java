buildscript {
    ext.grpc_version = '1.29.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
    }
}

plugins {
    id 'java'
    id 'com.google.protobuf' version "0.8.12"
    id 'idea'
}

group 'name.benjaminabbitt.evented'
version '0.0.0-SNAPSHOT'


repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/benjaminabbitt/maven"
    }
}


dependencies {
    //GRPC and Protobuf
    compile "com.google.protobuf:protobuf-java:3.11.4"
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile group: 'io.grpc', name: 'grpc-all', version: '1.29.0'


    //Protobuf Dependencies
    protobuf 'name.benjaminabbitt:evented-proto:0.0.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'

    //BDD
    testImplementation 'io.cucumber:cucumber-java8:5.6.0'
    testImplementation 'io.cucumber:cucumber-junit:5.6.0'
    testImplementation 'io.cucumber:cucumber-java:5.6.0'
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.11.4' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.29.0" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

idea {
    module {
        sourceDirs += file("$projectDir/src/generated/main/java")
        sourceDirs += file("$projectDir/src/generated/main/grpc")
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--strict', '--plugin', 'pretty', '--glue', 'name.benjaminabbitt.evented.java', 'src/test/resources']
        }
    }
}